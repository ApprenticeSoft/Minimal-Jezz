apply plugin: "com.android.application"

configurations {
    natives
}

android {
    namespace "com.minimal.jezz.android"
    compileSdk 35

    defaultConfig {
        applicationId "com.minimal.jezz.android"
        minSdk 23
        targetSdk 35
        versionCode 13
        versionName "1.12.0"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            aidl.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
            jniLibs.srcDirs = ["libs"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-project.txt"
        }
        debug {
            minifyEnabled false
        }
    }

    packaging {
        resources {
            excludes += [
                    "META-INF/DEPENDENCIES",
                    "META-INF/INDEX.LIST",
                    "META-INF/LICENSE*",
                    "META-INF/NOTICE*"
            ]
        }
    }
}

dependencies {
    implementation project(":core")

    implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"

    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"

    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"

    implementation "com.google.android.gms:play-services-ads:25.0.0"
}

tasks.register("copyAndroidNatives") {
    doLast {
        def libsDir = file("$projectDir/libs")
        delete(fileTree(libsDir) { include "**/*.so" })

        configurations.natives.files.each { nativeJar ->
            def jarName = nativeJar.name
            def abi = null
            if (jarName.contains("natives-arm64-v8a")) abi = "arm64-v8a"
            else if (jarName.contains("natives-armeabi-v7a")) abi = "armeabi-v7a"
            else if (jarName.contains("natives-x86_64")) abi = "x86_64"

            if (abi != null) {
                copy {
                    from zipTree(nativeJar)
                    include "*.so"
                    into file("$libsDir/$abi")
                }
            }
        }
    }
}

tasks.named("preBuild").configure {
    dependsOn("copyAndroidNatives")
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
